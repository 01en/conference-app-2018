apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 27
    dataBinding.enabled = true

    defaultConfig {
        applicationId "io.github.droidkaigi.confsched2018"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    signingConfigs {
        debug {
            storeFile rootProject.file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release {
            storeFile rootProject.file("release.keystore")
            storePassword System.getenv("BITRISEIO_ANDROID_KEYSTORE_PASSWORD")
            keyAlias System.getenv("BITRISEIO_ANDROID_KEYSTORE_ALIAS")
            keyPassword System.getenv("BITRISEIO_ANDROID_KEYSTORE_PRIVATE_KEY_PASSWORD")
        }
    }
    buildTypes {
        debug {
            resValue "string", "app_name", "DroidKaigi 2018 Dev"
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug"
        }
        release {
            resValue "string", "app_name", "DroidKaigi 2018"
            signingConfig signingConfigs.release
            debuggable false
            zipAlignEnabled true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation project(':common-jvm')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.3.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.3.0"
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'se.ansman.kotshi:api:0.3.0-beta1'
    kapt 'se.ansman.kotshi:compiler:0.3.0-beta1'

    // Android Architecture Components
    implementation 'android.arch.lifecycle:runtime:1.0.3'
    implementation 'android.arch.lifecycle:extensions:1.0.0'
    implementation 'android.arch.lifecycle:reactivestreams:1.0.0'
    implementation 'android.arch.persistence.room:runtime:1.0.0'
    implementation "android.arch.persistence.room:rxjava2:1.0.0"
    kapt 'android.arch.persistence.room:compiler:1.0.0'

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:2.1.6"
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'

    // OkHttp
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'

    // Support Library
    def supportLibraryVersion = '27.0.2'
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:support-vector-drawable:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0-beta3'

    // DataBinding
    kapt "com.android.databinding:compiler:3.0.1"

    // Dagger
    implementation 'com.google.dagger:dagger:2.13'
    implementation 'com.google.dagger:dagger-android:2.13'
    implementation 'com.google.dagger:dagger-android-support:2.13'
    kapt 'com.google.dagger:dagger-compiler:2.13'
    kapt 'com.google.dagger:dagger-android-processor:2.13'

    // Firebase
    implementation 'com.google.firebase:firebase-firestore:11.6.2'
    implementation 'com.google.firebase:firebase-auth:11.6.2'

    // ThreeTenABP for JSR-310 backport
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'

    // groupie for RecyclerView Adapter
    implementation 'com.xwray:groupie-databinding:2.0.0-alpha2'

    // MultiDex
    implementation 'com.android.support:multidex:1.0.2'

    // Timber
    implementation 'com.jakewharton.timber:timber:4.6.0'

    // LeakCanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'

    // Stetho
    implementation 'com.facebook.stetho:stetho:1.5.0'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.4.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.4.0'
    kapt 'com.github.bumptech.glide:compiler:4.4.0'

    // Test
    testImplementation 'junit:junit:4.12'
    testImplementation "com.nhaarman:mockito-kotlin:1.5.0"
    // Unit Test
    testImplementation "org.robolectric:robolectric:3.5.1"
    testImplementation 'com.willowtreeapps.assertk:assertk:0.9'
    // Instrumentation Test
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

}

repositories {
    mavenCentral()
}

apply plugin: 'com.google.gms.google-services'
